buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.0.3"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3"
    }
}
subprojects {
  apply plugin: 'java'
  apply plugin: 'nebula.optional-base'
  apply plugin: 'nebula.provided-base'
  
  targetCompatibility = "1.7"
  sourceCompatibility = "1.7"
  
  repositories {
      mavenCentral()
      jcenter()
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
      classifier = 'javadoc'
      from 'build/docs/javadoc'
  }
  
  task sourcesJar(type: Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
  }
  
  artifacts {
      archives jar
      archives javadocJar
      archives sourcesJar
  }
}


project(':discovery-java') {
    dependencies {
        compile project(':discovery-model-java')
    }
}

// Adding a Help task to debug the artifacts 
task artifacts {
  group = "Help"
  description = "Displays the artifacts associated with each configuration of " + project
  doFirst {
    configurations.findAll().each { config ->
      println "${config}:"
      config.allArtifacts.getFiles().each { file -> println "" + file}
      println ' '
    }
  }
}
